buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin'
    }
}

plugins {
    id 'maven-publish'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.71'
}

group = 'com.github.lazoyoung'
version = '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://repo.jenkins-ci.org/releases/' }
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://mvnrepository.com/artifact/org.codehaus.groovy/groovy-jsr223' }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.codehaus.groovy:groovy-groovydoc:3.0.2'
    implementation 'org.codehaus.groovy:groovy-groovysh:3.0.2'
    implementation 'org.codehaus.groovy:groovy-jsr223:3.0.2'
    compileOnly 'com.destroystokyo.paper:paper-api:1.15.2-R0.1-SNAPSHOT'
    compileOnly 'io.lumine.xikage:MythicMobs:4.9.0'
    compileOnly fileTree('libs')
    // compile 'org.graalvm.js:js:20.0.0' for JavaScript engine (GraalVM required)
}

idea.module {
    downloadJavadoc(true)
    downloadSources(true)
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

processResources {
    filesNotMatching("Sample/**") {
        expand('version': version)
    }
    setFilteringCharset('utf-8')
}

publishing {
    repositories {
        maven {
            name = "github"
            url = uri("https://maven.pkg.github.com/LazoYoung/CraftGames")
            credentials {
                username = getProperty('github.username')
                password = getProperty('github.token')
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}