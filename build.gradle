buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin"
    }
}

plugins {
    id "idea"
    id "maven-publish"
    id "org.jetbrains.dokka" version "0.10.1"
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "org.jetbrains.kotlin.jvm" version "1.3.71"
}

group = "com.github.lazoyoung"
version = "1.0-SNAPSHOT"

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
    maven {
        // VaultAPI
        url = "https://jitpack.io"
    }
    maven {
        // PaperSpigot
        url = "https://papermc.io/repo/repository/maven-public/"
    }
    maven {
        // LibsDisguises
        url = "http://repo.md-5.net/content/groups/public/"
        allowInsecureProtocol = true
    }
    maven {
        // ProtocolLib
        name = "dmulloy2-repo"
        url = "https://repo.dmulloy2.net/nexus/repository/public/"
    }
    maven {
        // Citizens, Denizen
        name = "everything"
        url = "https://repo.citizensnpcs.co/"
    }
    maven {
        // Shopkeeper
        url = "https://nexus.lichtspiele.org/repository/releases/"
    }
    maven {
        // WorldEdit
        url = "https://maven.enginehub.org/repo/"
    }
    maven {
        // Groovy
        url = "https://mvnrepository.com/artifact/org.codehaus.groovy/groovy-jsr223"
    }
    maven {
        // LootTablePatch
        name = "github"
        url = "https://maven.pkg.github.com/LazoYoung/LootTableFix"
        credentials {
            username = getProperty("github.username") ?: "undefined"
            password = getProperty("github.token") ?: "undefined"
        }
    }
    maven {
        // CommandAPI
        name = 'mccommandapi'
        url = 'https://raw.githubusercontent.com/JorelAli/1.13-Command-API/mvn-repo/1.13CommandAPI/'
    }
}

dependencies {
    // Dependency order is important!
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.codehaus.groovy:groovy:3.0.2"
    implementation "org.codehaus.groovy:groovy-jsr223:3.0.2"
    compileOnly "com.destroystokyo.paper:paper-api:1.15.2-R0.1-SNAPSHOT"
    compileOnly "com.github.lazoyoung:LootTableFix:1.0.3"
    compileOnly "com.sk89q.worldedit:worldedit-bukkit:7.1.0"
    compileOnly "net.citizensnpcs:citizens:2.0.26-SNAPSHOT"
    compileOnly "com.denizenscript:denizen:1.1.4-SNAPSHOT"
    compileOnly "com.nisovin.shopkeepers:Shopkeepers:2.9.3"
    compileOnly "LibsDisguises:LibsDisguises:10.0.5"
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    compileOnly "io.github.jorelali:commandapi:2.3"
}

idea.module {
    downloadJavadoc(true)
    downloadSources(true)
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

sourceSets {
    main {
        resources {
            srcDir "src/main/resources"
        }
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    filesNotMatching("Sample/**") {
        expand("version": version)
    }
    setFilteringCharset("utf-8")
}

dokka {
    outputFormat = "javadoc"
    outputDirectory = "docs"

    configuration {
        moduleName = "kdoc"
        targets = ["JVM"]
        platform = "JVM"
        jdkVersion = 8
        noStdlibLink = false
        noJdkLink = false

        kotlinTasks {
            defaultKotlinTasks()
        }

        sourceLink {
            path = './'
            url = "https://github.com/LazoYoung/CraftGames/tree/master"
            lineSuffix = '#L'
        }

        sourceRoot {
            path = "src/main/kotlin"
        }

        externalDocumentationLink {
            url = new URL("https://hub.spigotmc.org/javadocs/spigot/")
            packageListUrl = new URL("https://hub.spigotmc.org/javadocs/spigot/package-list")
        }

        externalDocumentationLink {
            url = new URL("https://ci.md-5.net/job/BungeeCord/ws/chat/target/apidocs/")
            packageListUrl = new URL("https://ci.md-5.net/job/BungeeCord/ws/chat/target/apidocs/package-list")
        }

        perPackageOption {
            prefix = "com.github.lazoyoung.craftgames.internal"
            suppress = true
        }

        perPackageOption {
            prefix = "com.github.lazoyoung.craftgames.command"
            suppress = true
        }

        perPackageOption {
            prefix = "com.github.lazoyoung.craftgames.game"
            suppress = true
        }

        perPackageOption {
            prefix = "com.github.lazoyoung.craftgames.coordtag"
            suppress = true
        }
    }
}

publishing {
    repositories {
        maven {
            name = "github"
            url = uri("https://maven.pkg.github.com/LazoYoung/CraftGames")
            credentials {
                username = getProperty("github.username") ?: "undefined"
                password = getProperty("github.token") ?: "undefined"
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}